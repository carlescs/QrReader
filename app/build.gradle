plugins {
    alias(libs.plugins.android.application)
    alias(libs.plugins.ksp)
    alias(libs.plugins.sonarqube)
    alias(libs.plugins.kotlin.compose)
    alias(libs.plugins.google.services)
    id 'jacoco'
}

// SonarCloud configuration is now centralized in sonar-project.properties

android {
    namespace = 'cat.company.qrreader'
    compileSdkVersion = libs.versions.targetSdk.get().toInteger()

    defaultConfig {
        applicationId = "cat.company.qrreader"
        minSdk = libs.versions.minSdk.get().toInteger()
        targetSdk = libs.versions.targetSdk.get().toInteger()
        
        // Use Git-based versioning
        versionCode = GitVersioning.getVersionCode(project)
        versionName = GitVersioning.getVersionName(project)

        testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary = true
        }
    }

    buildTypes {
        debug {
            testCoverageEnabled = true
        }
        release {
            minifyEnabled = false
            proguardFiles = [getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro']
        }
    }
    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_21
        targetCompatibility = JavaVersion.VERSION_21
    }
    buildFeatures {
        compose = true
    }
    composeOptions {
        kotlinCompilerExtensionVersion = libs.versions.composeCompiler.get()
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

dependencies {
    implementation libs.androidx.core.ktx
    implementation libs.androidx.lifecycle.runtime.ktx
    implementation libs.androidx.activity.compose
    implementation libs.bundles.compose
    implementation libs.androidx.lifecycle.viewmodel.compose
    implementation libs.androidx.print

    implementation libs.colorpicker.compose

    implementation libs.androidx.navigation.compose

    // Barcode model
    implementation libs.google.mlkit.barcode
    implementation libs.google.mlkit.genai.prompt

    // Google Play In-App Updates
    implementation libs.play.app.update.ktx

    implementation libs.qrcode.kotlin

    // CameraX core library using the camera2 implementation
    implementation libs.bundles.camerax

    implementation libs.accompanist.permissions

    implementation libs.bundles.room
    annotationProcessor libs.androidx.room.compiler

    // Firebase Analytics
    implementation libs.firebase.analytics

    // To use Kotlin Symbol Processing (KSP)
    ksp libs.androidx.room.compiler

    implementation libs.androidx.room.testing

    // Jetpack DataStore (Preferences)
    implementation libs.androidx.datastore.preferences

    // Koin for dependency injection
    implementation libs.koin.android
    implementation libs.koin.androidx.compose

    testImplementation libs.junit
    testImplementation libs.kotlinx.coroutines.test
    testImplementation libs.robolectric
    testImplementation libs.androidx.test.core
    testImplementation libs.koin.test
    testImplementation libs.koin.test.junit4
    testImplementation libs.play.app.update.testing
    androidTestImplementation libs.androidx.test.ext.junit
    androidTestImplementation libs.androidx.espresso.core
    androidTestImplementation libs.androidx.compose.ui.test.junit4
    debugImplementation libs.androidx.compose.ui.tooling
    debugImplementation libs.androidx.compose.ui.test.manifest
}

configurations.configureEach {
    resolutionStrategy {
        force "androidx.concurrent:concurrent-futures:1.2.0",
                "androidx.concurrent:concurrent-futures-ktx:1.2.0"
    }
}

// JaCoCo configuration for code coverage
jacoco {
    toolVersion = libs.versions.jacoco.get()
}

// Force a stable JaCoCo exec location when the unit test task exists
var unitTestTasks = tasks.withType(Test).matching { it.name == "testDebugUnitTest" }
unitTestTasks.configureEach {
    jacoco {
        enabled = true
        destinationFile = layout.buildDirectory.file("jacoco/testDebugUnitTest.exec").get().asFile
        includeNoLocationClasses = true
        excludes = ['jdk.internal.*']
    }
}

tasks.register('jacocoTestReport', JacocoReport) {
    dependsOn(unitTestTasks)
    dependsOn("assembleDebug")
    group = "Reporting"
    description = "Generate Jacoco coverage reports."

    reports {
        xml.required = true
        xml.outputLocation = layout.buildDirectory.file("reports/jacoco/jacocoTestReport/jacocoTestReport.xml")
        html.required = true
        csv.required = false
    }

    def fileFilter = [
            '**/R.class',
            '**/R$*.class',
            '**/BuildConfig.*',
            '**/Manifest*.*',
            '**/*Test*.*',
            'android/**/*.*',
            '**/*$ViewInjector*.*',
            '**/*$ViewBinder*.*',
            '**/databinding/**/*.*',
            '**/generated/**/*.*'
    ]

    def buildDir = layout.buildDirectory.get().asFile
    def mainSrc = "${project.projectDir}/src/main/java"

    sourceDirectories.setFrom(files([mainSrc]))

    // Use a provider to lazily resolve class directories at execution time
    classDirectories.setFrom(files().from({
        def debugTree = fileTree(dir: "${buildDir}/tmp/kotlin-classes/debug", excludes: fileFilter)
        def debugJavaTree = fileTree(dir: "${buildDir}/intermediates/javac/debug/classes", excludes: fileFilter)
        def compileJarPath = file("${buildDir}/intermediates/compile_app_classes_jar/debug/bundleDebugClassesToCompileJar/classes.jar")
        def runtimeJarPath = file("${buildDir}/intermediates/runtime_app_classes_jar/debug/bundleDebugClassesToRuntimeJar/classes.jar")

        def allClasses = [debugTree, debugJavaTree]

        if (compileJarPath.exists()) {
            allClasses.add(zipTree(compileJarPath).matching { exclude fileFilter })
        }
        if (runtimeJarPath.exists()) {
            allClasses.add(zipTree(runtimeJarPath).matching { exclude fileFilter })
        }

        return allClasses
    }))

    // Collect exec files from common AGP locations
    executionData.setFrom(fileTree(dir: buildDir, includes: [
            "jacoco/*.exec",
            "outputs/unit_test_code_coverage/**/*.exec"
    ]))
}

// Tasks to print version info for CI/CD
// Access version information directly from GitVersioning (same source as android.defaultConfig)
// Capture values at configuration time for configuration cache compatibility
def versionCode = GitVersioning.getVersionCode(project)
def versionName = GitVersioning.getVersionName(project)

tasks.register("printVersionCode") {
    doLast {
        println(versionCode)
    }
}

tasks.register("printVersionName") {
    doLast {
        println(versionName)
    }
}
